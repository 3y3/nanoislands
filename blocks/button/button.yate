func nb-button(nodeset options) {
    default = {
        'size': 'm'
        'theme': 'normal'
    }

    if options.type == 'file' {
        nb-block('buttonAttach', nb-extend(default, options))
    } else if options.type == 'span' {
        nb-block('buttonSpan', nb-extend(default, options))
    } else if options.href {
        nb-block('buttonLink', nb-extend(default, options))
    } else if options.for {
        nb-block('buttonLabel', nb-extend(default, options))
    } else {
        nb-block('button', nb-extend(default, options))
    }
}

func _nb-button-attributes(type) {
    @class = "nb-button"

    if (type != 'label') && (type != 'span') {
        @class += ' _init'

        if !( .static ) {
            @data-nb = 'button'
        }
    }

    // Set the size modifier of the button
    size = if .size == 's' {
        '-small'
    } else if .size == 'm' {
        if .theme == 'normal' {
            '-normal'
        } else {
            ''
        }
    } else if .size == 'l' {
        '-large'
    } else if .size == 'xl' {
        '-extra-large'
    }

    // Set the theme modifier of the button
    theme = if .theme == 'normal' {
        ''
    } else {
        '-' + .theme
    }

    // Apply the modifier
    @class += ' nb' + size + theme + '-button'

    if .disabled {
        @class += ' nb-is-disabled'

        if type == 'button' {
            @disabled = 'disabled'
        }
    } else if type == 'link' {
            @href="{ .href }"
    } else if type == 'label' {
            @for="{ .for }"
    }

    if type == 'attach' {
        @class += ' nb-attach-button'
    }

    if .checked {
        @class += ' is-checked'
    }

    if type == 'button' {
        if .name {
            @name = .name
        }

        if .attrs.name {
            @name = .attrs.name
        }
    }

    if !( .content ) && (.icon || .iconText) {
        @class += ' nb-with-only-button'
    }

}

func _nb-button-content() {
    if .iconText {
        <span class="nb-icon nb-icon_text">
          .iconText
        </span>
    }
    if .content {
        html(.content)
    }
}

match .button nb {
    <button>
        _nb-button-attributes('button')

        apply . nb-main-attrs

        <span class="nb-button-content">
            apply .icon nb

            _nb-button-content()
        </span>
    </button>
}

match .buttonLink nb {
    <a>
        _nb-button-attributes('link')

        apply . nb-main-attrs

        <span class="nb-button-content">
            apply .icon nb

            _nb-button-content()
        </span>
    </a>
}

match .buttonSpan nb {
    <span>
        _nb-button-attributes('span')

        apply . nb-main-attrs

        <span class="nb-button-content">
            apply .icon nb

            _nb-button-content()
        </span>
    </span>
}

match .buttonLabel nb {
    <label>
        _nb-button-attributes('label')

        apply . nb-main-attrs

        <span class="nb-button-content">
            apply .icon nb

            _nb-button-content()
        </span>
    </label>
}

match .buttonAttach nb {
    <label>
        _nb-button-attributes('attach')

        apply . nb-main-attrs

        <span class="nb-file-intruder">
            <span class="nb-file-intruder__inner">
                <input class="nb-file-intruder__input" type="file">
                    if .attrs.multiple || .multiple {
                        @multiple = "multiple"
                    }

                    if .name {
                        @name = .name
                    }

                    if .attrs.name {
                        @name = "{.attrs.name}"
                    }
                    if .attrs.title {
                        @title += "{.attrs.title}"
                    }

                    if .disabled {
                        @disabled = 'disabled'
                    }
                </input>
                <span class="nb-file-intruder__focus"></span>
            </span>
        </span>
        <span class="nb-button-content">
            apply .icon nb

            _nb-button-content()
        </span>
    </label>
}
