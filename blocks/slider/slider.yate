func nb-slider(nodeset options) {
    default = {
        'value': 0
        'type': 'range'
        'size': 's'
        'orientation': 'horiz'
    }

    _nb-block('slider', _nb-deep-extend(default, options))
}

match .slider nb {
    <div>
        _size-theme-type-class({
            'name': 'slider'
            'size': .size
         })
        @class += " _init _nb-{ .orientation }-slider _nb-slider-{ .type }-handle"

        apply . nb-main-attrs

        @data-nb-value = '{ .value }'

        if !.static {
            @data-nb = 'slider'
        }

        if .disabled {
            @class += ' _nb-is-disabled'
        }

        <div class='_nb-slider-body _nb-slider-{ .type }-body'>
            <label class='ui-slider-handle'>
                if .size == 's' {
                    @class += ' _nb-slider-small-handle'
                } else {
                    @class += ' _nb-slider-handle'
                }

                if .handle.class {
                    @class += ' { .handle.class }'
                }

                if (.tabindex && !.disabled) {
                    @tabindex = .tabindex
                }

                <span class='nb-button-content'>
                    if .content {
                        @class += ' nb-is-hidden'
                    }
                    <input tabindex="-1" class='_nb-slider-fallback' type='text' value='{ .value }' readonly='readonly'>
                        if .name {
                            @name = '{ .name }'
                        }
                    </input>
                </span>

                if .content {
                    html(.content)
                }
            </label>
        </div>
    </div>
}
