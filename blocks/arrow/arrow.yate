func nb-arrow(nodeset arrow) {
    default = {
        'search': 'true'
        'buttonContent': 'Найти'
    }

    nb-block('arrow', nb-extend(default, arrow))
}

match .arrow nb {
    <div>
        @id = .id
        @data-nb = 'arrow'
        @class = 'nb-arrow _init'
        if .search != 'true'{
            @class += ' nb-arrow_service'
        } else {
            @class += ' nb-arrow_search'
        }
        <div class="nb-arrow_top"></div>
        <div class="nb-arrow_bottom"></div>
        <div class="nb-arrow__content">
            apply . nb-main-content
        </div>
    </div>
}


match .arrow nb-content {
    if .search != 'true' {
        @class += ' nb-arrow__service'
        <a href="{.href}" class="_link nb-arrow__service__name">{.text}</a>
    } else {
        @class += ' nb-arrow__search'
        input = {
            'size': 'm'
            'class': 'nb-arrow__input'
            'attrs': {
                'autocomplete': 'off'
                'value': .value
            }
        }
        button = {
            'size': 'm'
            'content': .buttonContent
            'class': 'nb-arrow__button'
        }

        nb-button(button)
        if .text {
        <div class="nb-arrow__name__wrap">
            <a href="{.href}" class="nb-arrow__name _link">{.text}</a>
        </div>
        }
        <div class="nb-arrow__input__wrap">
            nb-input(input)
        </div>

        if .requests {
            <div class="nb-arrow__requests__wrap">
                <span class="nb-arrow__input_fake"> првиет</span>
                <div class="nb-arrow__requests"> &mdash; {.requests} </div>
            </div>
        }


    }

}