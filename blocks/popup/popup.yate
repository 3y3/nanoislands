func nb-popup-menu(nodeset options) {
    default = {
       'none': 'none'
       'tail': 'left|right|top|bottom'
       //'menu': [
       //    {
       //        'href': '#'
       //        'text': 'Скопировать'
       //     }
       //]
    }

    nb-block('popupMenu', nb-extend(default, options))
}

match .popupMenu nb {
    <div class="nb-popup _hidden" data-nb='popup'>

        apply . nb-main-attrs

        if .tail {
            @data-nb-tail = .tail
        }

        for .menu {
            <a href="{ .href }" class="nb-popup__line _link">
                .text
            </a>
        }
    </div>
}


func nb-popup(nodeset options) {
    default = {
       'none': 'none'
        // 'title': 'Some modal popup'
        // 'content': 'Some text for that popup'
    }

    nb-block('popup', nb-extend(default, options))
}

match .popup nb {
    <div class="nb-popup _hidden" data-nb='popup'>

        apply . nb-main-attrs

        if .tail {
            @data-nb-tail = .tail
        }

        if .title {
            <div class="nb-popup__title">
                .title
            </div>
        }

        <div class="nb-popup__content">
            html(.content)
        </div>

    </div>
}

func nb-popup-modal(nodeset options) {
    default = {
        'data-nb': {
            'modal': 'true'
            'width': 'auto'
            'height': 'auto'
            'minHeight': 'auto'
        }
        'close': 'true'
     // 'title': 'Some modal popup'
     // 'content': 'Some text for that popup'
     // 'buttons':
    }

    nb-block('modalPopup', nb-deep-extend(default, options))
}

match .modalPopup nb {
    <div class="nb-popup nb-popup_type_modal _hidden" data-nb="popup">
        apply . nb-main-attrs

        if .close {
            <a class="nb-popup__close">
                nb-icon('close_16')
            </a>
        }

        if .title {
            <div class="nb-popup__title">
                .title
            </div>
        }

        <div class="nb-popup__content">
            .content
        </div>

        if .buttons {
            <div class="nb-popup__buttons">
                for .buttons {
                    nb-button(.)
                }
            </div>
        }
    </div>
}
