func nb-button(nodeset options) {
    default = {
        'size': 'm'
        'theme': 'normal'
    }

    if options.type == 'file' {
        nb-block('buttonAttach', nb-extend(default, options))
    } else if options.href {
        nb-block('buttonLink', nb-extend(default, options))
    } else {
        nb-block('button', nb-extend(default, options))
    }
}

func _nb-button-attributes(type) {
    @class = "nb-button _init"

    if !( .static ) {
        @data-nb = 'button'
    }

    // Set the size modifier of the button
    size = if .size == 's' {
        '-small'
    } else if .size == 'm' {
        if .theme == 'normal' {
            '-normal'
        } else {
            ''
        }
    } else if .size == 'l' {
        '-large'
    } else if .size == 'xl' {
        '-extra-large'
    }

    // Set the theme modifier of the button
    theme = if .theme == 'normal' {
        ''
    } else {
        '-' + .theme
    }

    // Apply the modifier
    @class += ' nb' + size + theme + '-button'

    if .disabled {
        @class += ' nb-is-disabled'

        if type == 'button' {
            @disabled = 'disabled'
        }
    } else {
        if type == 'link' && .href {
            @href="{ .href }"
        }
    }

    if type == 'attach' {
        @class += 'nb-button_type_attach'
    }

    if type == 'button' {
        if .name {
            @name = .name
        }

        if .attrs.name {
            @name = .attrs.name
        }
    }

    if !( .content ) && (.icon || .iconText) {
        @class += ' nb-button_only-icon'
    }

}

func _nb-button-content() {
    if .iconText {
        <span class="nb-icon nb-icon_text">
          .iconText
        </span>
    }
    if .content {
        html(.content)
    }
}

match .button nb {
    <button>
        apply . nb-main-attrs

        _nb-button-attributes('button')

        <span class="nb-button-content">
            apply .icon nb

            _nb-button-content()
        </span>
    </button>
}

match .buttonLink nb {
    <a>
        apply . nb-main-attrs

        _nb-button-attributes('link')

        <span class="nb-button-content">
            apply .icon nb

            _nb-button-content()
        </span>
    </a>
}

match .buttonAttach nb {
    <label>
        apply . nb-main-attrs

        _nb-button-attributes('attach')

        <span class="nb-file-intruder">
            <span class="nb-file-intruder__inner">
                <input class="nb-file-intruder__input" type="file">
                    if .attrs.multiple || .multiple {
                        @multiple = "multiple"
                    }

                    if .name {
                        @name = .name
                    }

                    if .attrs.name {
                        @name = "{.attrs.name}"
                    }
                    if .attrs.title {
                        @title += "{.attrs.title}"
                    }

                    if .disabled {
                        @disabled = 'disabled'
                    }
                </input>
                <span class="nb-file-intruder__focus"></span>
            </span>
        </span>
        <span class="nb-button-content">
            apply .icon nb

            _nb-button-content()
        </span>
    </label>
}
