func nb-input(nodeset options) {
    default = {
        'size': 's'
    }

    if options.fieldLeft || options.fieldRight || options.fieldLabel {
        nb-block('input', nb-extend(default, options))
    } else {
        nb-block('inputSimple', nb-extend(default, options))
    }
}

match .input nb {
    <span data-nb='input' class='nb-input'>
        apply . nb-main-attrs
        if .size {
            @class += ' nb-input_size_{.size}'
        }

        if .disabled {
            @class += ' nb-input_disabled'
            @data-nb-disabled = 'true'
        }

        <input value="{.content}" class="nb-input__input">
            for .attrs.* {
                @{name()} = .
            }

            if .disabled {
                @disabled = 'disabled'
            }

            apply .* nb-content
        </input>
    </span>
}
match .inputSimple nb {
    <input data-nb='input' data-nb-type='simple' class='nb-input nb-input_type_simple'>
        apply . nb-main-attrs

        if .size {
            @class += ' nb-input_size_{.size}'
        }

        if .disabled {
            @class += ' is-disabled'
            @disabled = 'disabled'
            @data-nb-disabled = 'true'
        }
    </input>
}
