func nb-checkbox(nodeset options) {
    default = {
        'size': 'm'
        'type': 'checkbox'
    }

    nb-block('checkbox', nb-extend(default, options))
}

match .checkbox nb {
    uniq = 'nb-checkbox_{nb-uniq()}'

    <label class="nb-checkbox nb-checkbox_size_{ .size } nb-checkbox_type_{ .type }" for="{ uniq }">
        <input class="nb-checkbox__input" type="{ .type }" id="{ uniq }">
            apply . nb-main-attrs

            if .checked {
                @checked = 'checked'
            }
        </input>
        <span class="nb-checkbox__flag"><span class="nb-checkbox__flag__icon"></span></span>

        <span class="nb-checkbox__label">
            .text
        </span>
    </label>
}

