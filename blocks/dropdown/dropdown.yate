func nb-dropdown(nodeset options) {
    default = {
        'direction': 'down'
        'tail': 'to-bottom'
    }

    nb-block('dropdown', nb-extend(default, options))
}

match .dropdown nb {
    <div>
        apply . nb-main-attrs
        apply . nb-main-content
    </div>
}

match .dropdown nb-attrs {
    @data-nb = 'dropdown'
    @class = 'nb-dropdown'
}


match .dropdown.direction nb-attrs {
    @class += ' nb-dropdown_direction_{.}'
}

match .dropdown.tail nb-attrs {
    @class += ' nb-dropdown_tail_{.}'
}

match .dropdown nb-content {
    apply .* nb-content
}

match .dropdown.tail nb-content {
    <div class="nb-dropdown__tail"></div>
}


match .dropdown.menu nb-content {
    <a href="{.href}" class="nb-dropdown__line">
        .text
    </a>
}



