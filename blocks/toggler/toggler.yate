func nb-toggler(nodeset toggler) {
    default = {
        'leftText': 'On'
        'rightText': 'Off'
        'size': 's'
        //'disabled': true()
        //'checked': true()
    }

    _nb-block('toggler', _nb-extend(default, toggler))
}

match .toggler nb {
    <a data-nb="toggler">
     _size-theme-type-class({
        'name': 'toggler'
        'size': .size
    })
    @class += " _init"

        apply . nb-main-attrs
        if .checked {
            @class += ' _nb-is-checked'
        }

        if .tabindex {
            @tabindex = .tabindex
        }

        if .disabled {
            @class += ' _nb-is-disabled'
        }
        <label class="_nb-toggler-label">
            <input type="checkbox" class="_nb-toggler-checkbox" >
                for .attrs.* {
                   @{name()} = .
                }

                if .name {
                    @name = .name
                }

                if .checked {
                    @checked = 'checked'
                }

                if .disabled {
                    @disabled = 'disabled'
                }
            </input>
        </label>
        <span class="_nb-toggler-container">
            <span class="_nb-toggler-sticker _nb-toggler-left-sticker">
                <span class="_nb-toggler-text">
                    .leftText
                </span>
            </span>
            <span class="_nb-toggler-sticker _nb-toggler-right-sticker">
                <span class="_nb-toggler-text">
                    .rightText
                </span>
            </span>
        </span>
        <span class="_nb-toggler-knob"></span>
    </a>
}
