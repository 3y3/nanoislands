func nb-checkbox(nodeset options) {
    default = {
        'size': 'm'
        'type': 'checkbox'
        'checked': false()
    }

    nb-block('checkbox', nb-extend(default, options))
}

match .checkbox nb {
    uniq = 'nb-checkbox_{nb-uniq()}'

    <label class="nb-checkbox" for="{uniq}">
        if .size == 's' {
            @class += ' nb-checkbox_size_s'
        }

        <input class="nb-checkbox__input" type="{.type}" id="{uniq}">
            if .name {
                @name = .name
            }
            if .value {
                @value = .value
            }
            if .checked {
                @checked = 'checked'
            }
        </input>
        <span class="nb-checkbox__flag nb-checkbox__flag_{.type}"></span>
        ' '
        .text
    </label>
}