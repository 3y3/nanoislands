func nb-button(nodeset button) {
    block = {
        'block': 'button'
        'size': 'm'
    }

    apply nb-extend(block, button) nb
}

match .*[.block == 'button'] nb {
    <button>
        apply . nb-main
    </button>
}

match .*[.block == 'button' && .href] nb {
    <a href="{.href}">
        apply . nb-main
    </a>
}

match .*[.block == 'button'] nb-attrs {
    @class = 'nb-button'

    if (.size) {
        @class += ' nb-button_size_{.size}'
    }

    if (.theme) {
        @class += ' nb-button_theme_{.theme}'
    }

    if (.class) {
        @class += ' {.class}'
    }

    if .disabled {
        @class += ' nb-button_disabled js-disabled'
    }
}

match .*[.block == 'button'] nb-content {
    <span class="nb-button__text">
        .content
    </span>
}
