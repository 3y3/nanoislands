func nb-header(nodeset header) {
    default = {
        'arrow': {
            'search': 'true'
            'buttonContent': 'Найти'
            'class': '_nb-header-arrow'
        }
        'services': true()
        'settings': true()
        'yaHref': 'http://yandex.ru'
    }

    _nb-block('header', _nb-deep-extend(default, header))
}

match .header nb {
    <div class="nb-header _init" data-nb="header">
        apply . nb-main-attrs
        <div class="_nb-header-actions">

            if .settings {
                if .settings.content {
                    html(.settings.content)
                } else {
                     <div class="_nb-header-button">
                        <div class="_nb-header-button-icon">
                            nb-icon({
                                'icon': 'settings'
                                'size': 'm'
                            })
                        </div>
                    </div>
                }
            }

            if .services {
                if .services.content {
                    html(.auth.content)
                } else {
                    <div class="_nb-header-button">
                        <div class="_nb-header-button-icon">
                            nb-icon({
                                'icon': 'services'
                                'size': 'm'
                            })
                        </div>
                    </div>
                }
            }

            if .user {
                if .user.content {
                    html(.user.content)
                } else {
                    nb-user(.user)
                }
            }

            if .auth {
                if .auth.content {
                    html(.auth.content)
                }
            }
        </div>
        <div class="_nb-header-main">
            <a href="{.yaHref}" class="_nb-header-logo"></a>
            nb-arrow(.arrow)
        </div>
    </div>
}